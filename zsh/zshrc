### Added by Zinit's installer
if [[ ! -f $HOME/.local/share/zinit/zinit.git/zinit.zsh ]]; then
    print -P "%F{33} %F{220}Installing %F{33}ZDHARMA-CONTINUUM%F{220} Initiative Plugin Manager (%F{33}zdharma-continuum/zinit%F{220})â€¦%f"
    command mkdir -p "$HOME/.local/share/zinit" && command chmod g-rwX "$HOME/.local/share/zinit"
    command git clone https://github.com/zdharma-continuum/zinit "$HOME/.local/share/zinit/zinit.git" && \
        print -P "%F{33} %F{34}Installation successful.%f%b" || \
        print -P "%F{160} The clone has failed.%f%b"
fi

source "$HOME/.local/share/zinit/zinit.git/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit
### End of Zinit's installer chunk

# Environment Setup
export LANG=en_US.UTF-8
export LC_ALL=en_US.UTF-8
export EDITOR=$(command -v nvim >/dev/null && echo "nvim" || echo "vim")
[[ $EDITOR == "nvim" ]] && alias vim="nvim"
command -v codium &> /dev/null && alias code="codium"

# Add local bin to path
path+=$HOME/.local/bin

# Set LS_COLORS for file type coloring
if command -v vivid &> /dev/null; then
    export LS_COLORS="$(vivid generate catppuccin-mocha)"
fi

# Load Custom Aliases
source ~/.config/zsh/aliases

# Allow aliases to work with sudo
alias sudo='sudo '

# Vi mode
bindkey -v
export KEYTIMEOUT=1  # Fast mode switching (10ms)

# Keybindings (set after vi mode to override defaults)
bindkey '^p' history-search-backward
bindkey '^n' history-search-forward
bindkey '^\[w' kill-region

# History
HISTSIZE=5000
HISTFILE=~/.zsh_history
SAVEHIST=$HISTSIZE
HISTDUP=erase
setopt appendhistory
setopt sharehistory
setopt hist_ignore_space
setopt hist_ignore_all_dups
setopt hist_ignore_dups
setopt hist_save_no_dups
setopt hist_find_no_dups
setopt auto_cd

# Completion styling
zstyle ':completion:*' matcher-list 'm:{a-z}={A-Za-z}'
zstyle ':completion:*' list-colors "${(s.:.)LS_COLORS}"
zstyle ':completion:*' menu no

# Load Custom Host Configurations
[[ -f "$HOME/.config/zsh/hosts" ]] && source "$HOME/.config/zsh/hosts" && alias hosts="cat $HOME/.config/zsh/hosts"

# ZSH Plugins - Loaded with turbo mode for faster startup
zinit wait lucid light-mode for \
    atinit"zicompinit; zicdreplay" \
        zdharma-continuum/fast-syntax-highlighting \
    atload"_zsh_autosuggest_start" \
        zsh-users/zsh-autosuggestions \
    blockf atpull'zinit creinstall -q .' \
        zsh-users/zsh-completions

# Platform-specific configurations
if [[ $(uname) == "Darwin" ]]; then
    [[ -f "$HOME/.config/zsh/profile-macos" ]] && source "$HOME/.config/zsh/profile-macos"
elif [[ $(uname) == "Linux" ]]; then
    [[ -f "$HOME/.config/zsh/profile-linux" ]] && source "$HOME/.config/zsh/profile-linux"
fi

# fzf
if command -v fzf &> /dev/null; then
    eval "$(fzf --zsh)"

    # Catppuccin Mocha theme for fzf
    export FZF_DEFAULT_OPTS="--height=80% \
--color=bg+:#313244,bg:#1e1e2e,spinner:#f5e0dc,hl:#f38ba8 \
--color=fg:#cdd6f4,header:#f38ba8,info:#cba6f7,pointer:#f5e0dc \
--color=marker:#f5e0dc,fg+:#cdd6f4,prompt:#cba6f7,hl+:#f38ba8"

    zinit light Aloxaf/fzf-tab

    # Configure fzf-tab with Catppuccin colors
    zstyle ':fzf-tab:*' fzf-flags --height=80% --color=bg+:#313244,bg:#1e1e2e,spinner:#f5e0dc,hl:#f38ba8 --color=fg:#cdd6f4,header:#f38ba8,info:#cba6f7,pointer:#f5e0dc --color=marker:#f5e0dc,fg+:#cdd6f4,prompt:#cba6f7,hl+:#f38ba8
    zstyle ':fzf-tab:complete:cd:*' fzf-preview 'ls --color $realpath'
fi

# zoxide
if command -v zoxide &> /dev/null; then
    eval "$(zoxide init --cmd cd zsh)"
    zstyle ':fzf-tab:complete:__zoxide_z:*' fzf-preview 'ls --color $realpath'
fi

# Add in snippets
zinit snippet OMZL::git.zsh
zinit snippet OMZP::git
zinit snippet OMZP::sudo
zinit snippet OMZP::docker
zinit snippet OMZP::aws
zinit snippet OMZP::kubectl
zinit snippet OMZP::kubectx
zinit snippet OMZP::command-not-found

# Development Environment Managers
if command -v pyenv &> /dev/null; then
    export PYENV_ROOT="$HOME/.pyenv"
    [[ -d $PYENV_ROOT/bin ]] && export PATH="$PYENV_ROOT/bin:$PATH"
    eval "$(pyenv init -)"
fi

if command -v nvm &> /dev/null; then
    export NVM_DIR="$HOME/.nvm"
    [ -s "$NVM_DIR/nvm.sh" ] && source "$NVM_DIR/nvm.sh"
    [ -s "$NVM_DIR/bash_completion" ] && source "$NVM_DIR/bash_completion"
fi

# TMUX Session Management
if command -v tmux &> /dev/null; then
    # Auto-start TMUX only in regular terminal
    # This function will be called after zsh initialization is complete
    _start_tmux_after_init() {
        # Only run this function once
        add-zsh-hook -d precmd _start_tmux_after_init

        # Only try to start tmux if we're not already in a tmux session
        # Skip auto-start if we're in an SSH session to prevent nesting
        if [[ -z "$TMUX" && -n "$PS1" && \
              -z "$SSH_CONNECTION" && \
              ! "$TERM" = *screen* && \
              ! "$TERM" = *tmux* && \
              "$TERM_PROGRAM" != "vscode" && \
              "$TERM_PROGRAM" != "cursor" ]]; then
            # Start tmux without producing output
            tmux new-session -A -s sesh >/dev/null 2>&1 || true
        fi
    }
    
    # Register the function to run after zsh initialization
    autoload -Uz add-zsh-hook
    add-zsh-hook precmd _start_tmux_after_init
fi

# Added by Windsurf
export PATH="/Users/jesse/.codeium/windsurf/bin:$PATH"

# Disable paste highlighting for cleaner pasting
zle_highlight=('paste:none')

# Initialize Starship prompt
eval "$(starship init zsh)"
